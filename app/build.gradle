apply plugin: 'com.android.application'

def buildTime() {
  return new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1

android {
  compileSdkVersion 22
  buildToolsVersion "23.0.0 rc3"

  defaultConfig {
    applicationId "org.maepaysoh.maepaysoh"
    minSdkVersion 15
    targetSdkVersion 22
    buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    debug {
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug" + "-build" + buildTime()
    }
  }
}

ext {
  //Version
  supportLibrary = '22.2.1'

  //Support Libraries dependencies
  supportDependencies = [design           : "com.android.support:design:${supportLibrary}",
                         recyclerView     : "com.android.support:recyclerview-v7:${supportLibrary}",
                         cardView         : "com.android.support:cardview-v7:${supportLibrary}",
                         appCompat        : "com.android.support:appcompat-v7:${supportLibrary}",
                         supportAnnotation: "com.android.support:support-annotations:${supportLibrary}",]
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile supportDependencies.appCompat
  compile supportDependencies.design
  compile supportDependencies.recyclerView
  compile supportDependencies.cardView
  compile 'com.squareup.retrofit:retrofit:1.9.0'
  compile 'com.squareup.okhttp:okhttp:2.4.0'
  compile 'com.github.bumptech.glide:glide:3.6.1'
  compile 'com.google.android.gms:play-services:7.5.0'
}
